plugins {
    id 'java'
    alias(libs.plugins.allure.report)
    alias(libs.plugins.allure.adpater)
    alias(libs.plugins.lombok)
}

group = libs.versions.group.get()
version = libs.versions.app.get()

repositories {
    gradlePluginPortal()
    mavenCentral()
}

allure {
    report {
        version.set("2.23.0")
    }
    adapter {
        autoconfigure.set(true)
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.23.0")
            }
        }
    }
}

dependencies {
    //TODO: Don't work via catalog - need fix
    implementation("jakarta.validation:jakarta.validation-api:3.0.2")
    implementation("jakarta.annotation:jakarta.annotation-api:1.3.5") // Java 9+ compatibility - Do NOT update to 2.0.0
    testImplementation(
            project(':rangiffler-grpc-common'),
            libs.junit5.jupiter,
            libs.bundles.selenide,
            libs.bundles.allure,
            libs.bundles.retrofit,
            libs.bundles.logs,
            libs.bundles.grpc,
            libs.bundles.database,
            libs.bundles.test.utils
    )
//    implementation 'org.aspectj:aspectjweaver:1.9.19'
//    implementation 'org.springframework:spring-aop:6.0.11'
}

// https://github.com/DiUS/java-faker/issues/327
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.module.toString() == 'org.yaml:snakeyaml') {
            details.artifactSelection {
                it.selectArtifact(DependencyArtifact.DEFAULT_TYPE, null, null)
            }
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}